buildscript {
    apply from: '../dependencies.gradle'
}

plugins {
    id "java-library"
    id "jacoco"
    id "io.freefair.aspectj.post-compile-weaving" version "${postCompileWeavingVersion}"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.amazonaws:aws-lambda-java-core:$dependencyVersions.awsLambdaJavaCore",
                   "com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2-instrumentor:$dependencyVersions.xrayInstrumentor",
                   "com.fasterxml.jackson.core:jackson-annotations:$dependencyVersions.jackson",
                   "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$dependencyVersions.jackson",
                   "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$dependencyVersions.jackson",
                   "org.aspectj:aspectjrt:$dependencyVersions.aspectjrt",
                   platform("software.amazon.awssdk:bom:$dependencyVersions.awsSdk"),
                   "software.amazon.awssdk:cognitoidentityprovider",
                   "software.amazon.awssdk:secretsmanager",
                   "software.amazon.awssdk:s3",
                   "software.amazon.awssdk:sns",
                   "software.amazon.awssdk:sqs"

    aspect "software.amazon.lambda:powertools-logging:$dependencyVersions.powertools",
           "software.amazon.lambda:powertools-tracing:$dependencyVersions.powertools"

    testImplementation "org.junit.jupiter:junit-jupiter:$dependencyVersions.junitJupiter",
                       "org.mockito:mockito-junit-jupiter:$dependencyVersions.mockitoJunitJupiter"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

test {
    environment "LAMBDA_TASK_ROOT", "handler"
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
    }
}

tasks.withType(Jar).configureEach { Jar jar ->
    jar.preserveFileTimestamps = false
    jar.reproducibleFileOrder = true
}

tasks.named("jar") {
    manifest {
        attributes("Implementation-Title": project.name,
                   "Implementation-Version": project.version)
    }
}
